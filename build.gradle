apply plugin: 'java'

version = getVersionFromTag()

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile 'nz.ac.waikato.cms.weka:weka-dev:3.9.5'
  compile group: 'com.google.guava', name: 'guava', version: '19.0'
  testCompile 'junit:junit:4.11',
  [group: 'nz.ac.waikato.cms.weka', name: 'weka-dev', classifier: 'tests', version: '3.9.5']
}

// JavaDoc with MathJax and core Weka docs linked
task javadoc(type: Javadoc, overwrite:true) {
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath

  // DO NOT use single quotes (') in the strings below, or you will get
  // weird errors like "Illegal package name" when you run the JavaDoc task
  options.header = "<script type=\"text/x-mathjax-config\">" +
  "  MathJax.Hub.Config({" +
  "     \"HTML-CSS\": {" +
  "       styles: {" +
  "         \".MathJax_Display\": {" +
  "            \"display\": \"inline\"," +
  "            \"text-align\": \"left\"" +
  "         }" +
  "       }" +
  "     }" +
  "  });" +
  "</script>" +
  "<script type=\"text/javascript\" src=\"http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_HTML\"></script>"
  options.links("http://weka.sourceforge.net/doc.dev/")
}

task dist(type:Zip){
    from jar.outputs.files
    from (javadoc.outputs.files) {
        into 'doc'
    }
    from (projectDir) {
    include ([
        'src/**/*',
        'data/**/*',
        'Description.props',
        'build.gradle',
        'LICENSE',
        'NOTICE',
        'README.mkdn',
        'todo.txt'])
    }
}

// Minor version bump; updates props file, commits, tags, and pushes
task bumpMinor {
    doLast {
        def newVersion = bumpVersion(version, 1)
        ant.input(message: "Bump version from " + version + " to " + newVersion + "?", validargs: 'y,n', addproperty: 'doVersionBump')
        if(ant.doVersionBump == 'n') {
            throw new GradleException("User stopped release at version bump step")
        }
        ant.replace(file: "Description.props", token: version, value: newVersion)
        version = newVersion
        gitAddAndCommit("bump version in Description.props")
        gitTag(version)
        gitPush()
    }
}

// HELPER METHODS

def bumpVersion(version, index) {
    def parts = version.split("[.]")
    parts[index] = Integer.toString(Integer.parseInt(parts[index]) + 1)
    for(int i = index+1; i < parts.length; i++) {
        parts[i] = "0"
    }
    def newVersion = parts.join(".")
}

def getVersionFromTag() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--abbrev=0'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def gitAddAndCommit(message) {
    exec {
        commandLine 'git', 'add', '.'
    }
    exec {
        commandLine 'git', 'commit', '-m', 'message'
    }
}

def gitPush(tagName) {
    exec {
        commandLine 'git', 'push'
    }
    exec {
        commandLine 'git', 'push', '--tags'
    }
}
